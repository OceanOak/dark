module Darklang =
  module Stdlib =
    module HttpClient =

      type HeaderError = | EmptyKey

      type BadUrlDetails =
        | UnsupportedProtocol
        | InvalidHost
        | InvalidUri
        | InvalidRequest

      type RequestError =
        | BadUrl of BadUrlDetails
        | Timeout
        | HeaderError of HeaderError
        | IOError of String
        | ConnectionError of String

      let toString (e: RequestError) : String =
        match e with
        | BadUrl details ->
          match details with
          | UnsupportedProtocol -> "Unsupported protocol"
          | InvalidHost -> "Invalid host"
          | InvalidUri -> "Invalid URI"
          | InvalidRequest -> "Invalid request"
        | Timeout -> "Timeout"
        | HeaderError details ->
          match details with
          | EmptyKey -> "Empty key"
        | IOError details -> $"IOError: {details}"
        | ConnectionError details -> $"Connection error: {details}"


      /// The response from a HTTP request
      type Response =
        { statusCode: Int
          headers: List<String * String>
          body: Bytes }


      /// Make blocking HTTP call to <param uri>. Returns a <type Result> where
      /// the response is wrapped in {{ Ok }} if a response was successfully
      /// received and parsed, and is wrapped in {{ Error }} otherwise
      let request
        (method: String)
        (uri: String)
        (headers: List<String * String>)
        (body: Bytes)
        : PACKAGE.Darklang.Stdlib.Result.Result<PACKAGE.Darklang.Stdlib.HttpClient.Response, PACKAGE.Darklang.Stdlib.HttpClient.RequestError> =
        Builtin.HttpClient.request method uri headers body


      /// Returns a header <type (String*String))> with {{'authorization'}} created using HTTP basic auth
      let basicAuth
        (username: String)
        (password: String)
        : PACKAGE.Darklang.Stdlib.Result.Result<(String * String), String> =
        if PACKAGE.Darklang.Stdlib.String.contains username "-" then
          PACKAGE.Darklang.Stdlib.Result.Result.Error
            "Username cannot contain a hyphen"
        else
          let encoded =
            PACKAGE.Darklang.Stdlib.String.base64Encode $"{username}:{password}"

          PACKAGE.Darklang.Stdlib.Result.Result.Ok(
            ("authorization", $"basic {encoded}")
          )

      /// Returns a header <type (String*String))> with {{'authorization'}} set to <param token>
      let bearerToken (token: String) : (String * String) =
        ("authorization", ("bearer " ++ token))


      module ContentType =
        /// A header <type (String*String))> with {{content-type}} set for HTML form requests or responses
        let form = ("content-type", "application/x-www-form-urlencoded")

        /// A header <type (String*String))> with {{content-type}} set for JSON requests or responses
        let json = ("content-type", "application/json")

        /// A header <type (String*String))> with {{'content-type'}} set for plain text requests or responses
        let plainText = ("content-type", "text/plain; charset=utf-8")

        /// A header <type (String*String))> with {{'content-type'}} set for html requests
        let html = ("content-type", "text/html; charset=utf-8")