module Darklang =
  module LanguageTools =
    module Parser =
      module ModuleDeclaration =
        let parseDecls
          (node: ParsedNode)
          : Stdlib.Result.Result<WrittenTypes.ModuleDeclaration.Declaration, WrittenTypes.Unparseable> =
          match node.typ with
          | "type_decl" ->
            (TypeDeclaration.parse node)
            |> Stdlib.Result.map (fun x ->
              WrittenTypes.ModuleDeclaration.Declaration.Type x)
          | "fn_decl" ->
            (FunctionDeclaration.parse node)
            |> Stdlib.Result.map (fun x ->
              WrittenTypes.ModuleDeclaration.Declaration.Function x)
          | "val_decl" ->
            (ValueDeclaration.parse node)
            |> Stdlib.Result.map (fun x ->
              WrittenTypes.ModuleDeclaration.Declaration.Value x)
          | "module_decl" ->
            (ModuleDeclaration.parse node)
            |> Stdlib.Result.map (fun x ->
              WrittenTypes.ModuleDeclaration.Declaration.SubModule x)
          | "expression" ->
            (Expr.parse node)
            |> Stdlib.Result.map (fun x ->
              WrittenTypes.ModuleDeclaration.Declaration.Expr x)
          | _ -> createUnparseableError node

        let parse
          (node: ParsedNode)
          : Stdlib.Result.Result<WrittenTypes.ModuleDeclaration.ModuleDeclaration, WrittenTypes.Unparseable> =
          if node.typ == "module_decl" then
            let keywordModule = findField node "keyword_module"
            let name = findField node "name"

            let declarations =
              (Stdlib.List.filter node.children (fun node ->
                node.typ == "module_content"))
              |> Stdlib.List.map (fun node ->
                match node.children |> Stdlib.List.head with
                | Some child -> ModuleDeclaration.parseDecls child
                | None -> createUnparseableError node)
              |> Stdlib.Result.collect

            match (keywordModule, name, declarations) with
            | Ok keywordModule, Ok name, Ok declarations ->
              (WrittenTypes.ModuleDeclaration.ModuleDeclaration
                { range = node.range
                  name = (name.range, name.text)
                  declarations = declarations
                  keywordModule = keywordModule.range })
              |> Stdlib.Result.Result.Ok

            | _ -> createUnparseableError node

          else
            createUnparseableError node